デザインレシピ
==============

データ定義
----------

入力データの型、出力データの型を考える。もし、それらの型が構造を持つ(かつ、まだ
定義されていない)ならその型を定義する。決してほかの型に埋め込んでしまうことはし
ないようにする。意味のあるかたまりに対してひとつの型を定義するのが望ましいプロ
グラミングスタイルである。もし、それらが再帰的なデータ型であった場合には、どこ
に再帰が含まれるかを確認する。また、自己参照しないケースがあることも確認する。

### e.g.

```ocaml
(* int list は
      - []               空リスト、あるいは
      - first :: rest    最初の要素が first で残りのリストが rest
                         (rest が自己参照のケース)
   という形 *)
```


目的
----

作成する関数が何をするものかを考える。何を受け取り、何を返すのかを考え、関数の
型を決定する。これをもとに関数の出だし、ヘッダを作成する。

### e.g.

```ocaml
(* 目的: 受け取ったリスト lst の各要素の和を求める *)
(* sum : int list -> int *)
let sum lst = 0
```


例
--

関数の動きをより明確かつ具体的にするため、作成する関数に望まれる入力と出力の例
を作成する。そして、それを実行可能なテストプログラムにする。いろいろな入力に
よって場合分けが必要になってくると予想される場合は、すべての場合について必ず例
を作成する。ある場合と別の場合の境界など微妙な場合の例も作成しておく。再帰的な
データの場合にも、すべての場合の例を用意する。特に、簡単に答えが出るケースと再
帰によるケースの両方の例を必ず含める。(リストの場合は、それぞれ空リストのケース
とそうでないケースになるが、データによってはどちらについても複数ある場合があ
る。)

### e.g.

```ocaml
(* テスト *)
let test1 = sum [] = 0
let test2 = sum [2] = 2
let test3 = sum [1; 3] = 4
let test4 = sum [1; 2; 3; 4; 5; 6; 7; 8; 9; 10] = 55
```


テンプレート
------------

入力(の一部)が構造を持つデータや再帰的なデータの場合は、その構造に応じたmatch文
を作成する。その際、どのようなパターン変数が使えるのかを確認する。再帰的なデー
タの場合は、どのパターン変数について再帰呼び出しを行えるかもチェックする。ま
た、この時点で条件分岐の形が明らかな場合には、場合分けの条件を特定して必要なif
文を作成する。

テンプレートの作成は、入力データの型さえわかれば機械的にできる。入力データの型
が定まるとプログラムの形の大枠は自然に定まる。

テンプレートができたら一度、実行してテストプログラムが動くことを確認しておく。

### e.g.

```ocaml
(* 目的: 受け取ったリスト lst の各要素の和を求める *)
(* sum : int list -> int *)
let sum lst = match lst with
    [] -> 0
  | first :: rest -> 0 (* sum rest *)
```


本体
----

関数の本体を作成する。目的のところでは関数が「何を」するのかを考えたが、ここで
はそれを「どうやって」実現するかを考える。場合分けがある場合は、簡単な場合から
ひとつひとつ順に作成する。再帰的な場合を作成するときには、再帰呼び出しの意味す
るところを目的を使って理解する。必要に応じて、作成した例を参照しながら行うのが
よいだろう。最後に、できあがった関数が再帰関数の場合は、letの後にrecを挿入す
る。

これまでの手順で、使える道具が明確になっている。例えば、使えるパターン変数や再
帰呼び出しができるのかといったこと。

### e.g.

```ocaml
(* 目的: 受け取ったリスト lst の各要素の和を求める *)
(* sum : int list -> int *)
let rec sum lst = match lst with
    [] -> 0
  | first :: rest -> first + sum rest
```


テスト
------

確かに望む動作をしているかを上で作ったテストプログラムを使って確認する。望む動
作をしていなかったら原因をデザインレシピに沿って考え、誤りを正す。

